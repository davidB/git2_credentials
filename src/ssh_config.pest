config = _{ SOI ~ ( newline | kv | in_comment )* ~ host* ~ ( newline | in_comment )* ~ EOI }
host = { ^"Host" ~ pattern+ ~ ( option | host_comment )* ~ newline ~ ( newline | in_comment )* }
kv = { key ~ value }
option = { newline ~ kv}
host_comment = { newline ~ in_comment }
key = @{ !^"Host" ~ ('a'..'z' | 'A'..'Z' | ASCII_DIGIT)+ }
value = ${ value_unquoted | value_quoted }
value_unquoted = { (!newline ~ !dquote ~ ANY)+ }
value_quoted = _{ dquote ~ values ~ dquote }
values = _{(value_unquoted)*}

pattern = ${ "!"? ~ (ASCII_DIGIT | 'a'..'z' | 'A'..'Z' | "-" | "_" | "." | "*")+ }

dquote = _{ "\"" }
newline = _{ "\n" | "\r\n" }

WHITESPACE = _{ " " | "\t" }
in_comment = { "#" ~ (!newline ~ ANY)* }
