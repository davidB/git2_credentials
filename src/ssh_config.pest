config = _{ SOI ~ (newline | kv)* ~ host* ~ EOI }
host = { ^"Host" ~ pattern+ ~ option* ~ newline* }
kv = { key ~ value }
option = { newline* ~ kv}
key = @{ !^"Host" ~ ('a'..'z' | 'A'..'Z' | ASCII_DIGIT)+ }
value = ${ value_unquoted | value_quoted }
value_unquoted = { (!newline ~ !dquote ~ ANY)+ }
value_quoted = _{ dquote ~ values ~ dquote }
values = _{(value_unquoted)*}

pattern = ${ "!"? ~ (ASCII_DIGIT | 'a'..'z' | 'A'..'Z' | "-" | "_" | "." | "*")+ }

dquote = _{ "\"" }
newline = { _COMMENT? ~ NEWLINE }

WHITESPACE = _{ " " | "\t" }
_COMMENT   = _{ WHITESPACE* ~ "#" ~ (!NEWLINE ~ ANY)* }